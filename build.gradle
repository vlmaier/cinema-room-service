buildscript {
    ext.kotlin_version = '1.8.20'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

subprojects {
    apply plugin: 'application'
    apply plugin: 'java'
    apply plugin: 'kotlin'

    sourceCompatibility = 11

    repositories {
        mavenCentral()

        maven { url "https://jitpack.io" }
    }

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
        implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
        testImplementation 'com.github.hyperskill:hs-test:master-SNAPSHOT'
    }

    configurations.configureEach {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "11"
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = "11"
    }

    sourceSets {
        main {
            kotlin.srcDir 'src'
            java.srcDir 'src'
        }
        test {
            kotlin.srcDir 'test'
            java.srcDir 'test'
        }
    }

    test {
        systemProperty "file.encoding", "utf-8"
    }

    compileJava.options.encoding = 'utf-8'
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.encoding = 'utf-8'
        }
    }
}

project(':util') {
    dependencies {
        implementation 'com.github.hyperskill:hs-test:master-SNAPSHOT'
    }
}

configure(subprojects.findAll { it.name != 'util' }) {
    dependencies {
        testImplementation project(':util').sourceSets.main.output
        testImplementation project(':util').sourceSets.test.output
    }
}
